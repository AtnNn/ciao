<HTML>
<HEAD>
<!-- Created by texi2html 1.56k + clip patches and <A href="http://www.clip.dia.fi.upm.es/Software">lpdoc</A> from ciao.texi on 10 November 2000 -->

<LINK rel="stylesheet" href="ciao.css" type="text/css">
<TITLE>The Ciao Prolog System               - Using the persdb library</TITLE>
</HEAD>
<BODY> 
Go to the <A HREF="ciao_1.html">first</A>, <A HREF="ciao_113.html">previous</A>, <A HREF="ciao_115.html">next</A>, <A HREF="ciao_175.html">last</A> section, <A HREF="ciao_toc.html">table of contents</A>.
<P><HR><P>


<H1><A NAME="SEC457" HREF="ciao_toc.html#TOC457">Using the persdb library</A></H1>

<P>
Through the following examples we will try to illustrate the two mains ways of declaring and using persistent predicates: statically (the preferred method) and dynamically (necessary when the new persistent predicates have to be defined at run-time). The final example is a small application implementing a simple persistent queue. 



<UL>
<LI><A HREF="ciao_114.html#SEC458">An example of persistent predicates (static version)</A>
<LI><A HREF="ciao_114.html#SEC459">An example of persistent predicates (dynamic version)</A>
<LI><A HREF="ciao_114.html#SEC460">A simple application / a persistent queue</A>
</UL>



<H2><A NAME="SEC458" HREF="ciao_toc.html#TOC458">An example of persistent predicates (static version)</A></H2>


<PRE>
:- include(library(persdb)).

%% Required declarations for persistent_dir/2.
:- multifile persistent_dir/2.
:- data persistent_dir/2.

%% Declare the directory associated to the key "db" where the
%% persistence sets of the persistent predicates are stored:
persistent_dir(db,'./').

%% Declare a persistent predicate:
:- persistent(bar/1, db).

%% Read a term, storing it in a new fact of the persistent predicate
%% and list all the current facts of that predicate
main:-
     read(X),
     passertz_fact(bar(X)),
     findall(Y,bar(Y),L),
     write(L).

</PRE>



<H2><A NAME="SEC459" HREF="ciao_toc.html#TOC459">An example of persistent predicates (dynamic version)</A></H2>


<PRE>
:- include(library(persdb)).

%% Required declarations for persistent_dir/2.
:- multifile persistent_dir/2.
:- data persistent_dir/2.

main([X]):-
%%   Declare the directory associated to the key "db" 
     asserta_fact(persistent_dir(db,'./')),
%%   Declare the predicate bar/1 as dynamic (and data) at run-time  
     data(bar/1),
%%   Declare the predicate bar/1 as persistent at run-time  
     make_persistent(bar/1, db),
     passertz_fact(bar(X)),
     findall(Y, bar(Y), L),
     write(L).    

</PRE>



<H2><A NAME="SEC460" HREF="ciao_toc.html#TOC460">A simple application / a persistent queue</A></H2>

<PRE>
:- module(queue, [main/0],[]).

:- use_module(library(read)).
:- use_module(library(write)).
:- use_module(library(aggregates)).

:- include(library(persdb)).

:- multifile persistent_dir/2.
:- data persistent_dir/2.

:- persistent(queue/1, queue_dir).

persistent_dir(queue_dir,'./pers').

main:-
     write('Action ( in(Term). | out. | list. | halt. ): '),
     read(A),
     (  handle_action(A)
     -&#62; true
     ;  write('Unknown command.'), nl ),
     main.

handle_action(halt) :-
     halt.
handle_action(in(Term)) :-
     passertz_fact(queue(Term)),
     main.
handle_action(out) :-
     (  pretract_fact(queue(Term))
     -&#62; write('Out '), write(Term)
     ;  write('FIFO empty.') ),
     nl,
     main.
handle_action(list) :-
     findall(Term,pcurrent_fact(queue(Term)),Terms),
     write('Contents: '), write(Terms), nl,
     main.
     

</PRE>

<P><HR><P>
Go to the <A HREF="ciao_1.html">first</A>, <A HREF="ciao_113.html">previous</A>, <A HREF="ciao_115.html">next</A>, <A HREF="ciao_175.html">last</A> section, <A HREF="ciao_toc.html">table of contents</A>.
</BODY>
</HTML>
